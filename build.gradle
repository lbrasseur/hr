apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'appengine'
apply plugin: 'maven'

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.google.appengine:gradle-appengine-plugin:1.9.19'
	}
}

sourceCompatibility = 1.7
version = '0.1-SNAPSHOT'
group = 'com.aajtech.hr'

def appengineVersion = "1.9.19"
def vaadinVersion = "7.4.3"

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "http://maven.vaadin.com/vaadin-addons"
	}
}
	
appengine {
	httpPort = 8080
	downloadSdk = true
	appcfg {
		noCookies = true
		oauth2 = true
	}
	enhancer {
		version = "v2"
		api = "jpa"
		enhanceOnBuild = true
	}
}

task datanucleusEnhance {
    description "Enhance JPA model classes using DataNucleus Enhancer"
    dependsOn compileJava

    doLast {    
        // define the entity classes
        def entityFiles = fileTree(sourceSets.main.output.classesDir).matching {
            include 'com/aajtech/hr/model/*.class'
        }

        println "Enhancing with DataNucleus the following files"
        entityFiles.getFiles().each {
            println it
        }

        // define Ant task for DataNucleus Enhancer
        ant.taskdef(
            name : 'datanucleusenhancer',
            classpath : sourceSets.main.runtimeClasspath.asPath,
            classname : 'org.datanucleus.enhancer.EnhancerTask'
        )

        // run the DataNucleus Enhancer as an Ant task
        ant.datanucleusenhancer(
            classpath: sourceSets.main.runtimeClasspath.asPath,
            verbose: true,
            api: "JPA") {
            entityFiles.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
        }
    }
}

dependencies {
	appengineSdk "com.google.appengine:appengine-java-sdk:${appengineVersion}"
	compile "com.google.appengine:appengine-api-1.0-sdk:${appengineVersion}"
	compile('com.google.inject.extensions:guice-servlet:3.0') {
		exclude module: 'guice'
	}
	compile('com.google.inject:guice:3.0:no_aop') {
		exclude module: 'cglib'
	}
	compile 'com.google.guava:guava:18.0'
	compile 'com.google.code.findbugs:jsr305:2.0.3'
	compile 'com.vaadin.addon:jpacontainer:3.2.0'
	compile 'com.vaadin.addon:vaadin-touchkit-agpl:4.0.1'
	compile "com.vaadin:vaadin-client-compiled:${vaadinVersion}"
	compile "com.vaadin:vaadin-server:${vaadinVersion}"
	compile 'javax.inject:javax.inject:1'
	compile "javax.servlet:servlet-api:2.5"
	compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
	runtime 'org.datanucleus:datanucleus-accessplatform-jpa-rdbms:3.1.3'
	runtime 'org.datanucleus:datanucleus-enhancer:3.1.1'
}

tasks.eclipse.doLast {
	tasks.copyRuntimeLibs.execute()
}

task copyRuntimeLibs(type: Copy) {
    into "src/main/webapp/WEB-INF/lib"
    from configurations.compile
}